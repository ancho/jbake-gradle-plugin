/*
 * Copyright 2014-2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'com.jfrog.bintray'             version '1.7.3'
    id 'net.nemerosa.versioning'       version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.14.0'
    id 'com.gradle.plugin-publish'     version '0.9.5'
    id 'com.github.hierynomus.license' version '0.11.0'
    id 'com.github.kt3k.coveralls'     version '2.8.1'
}

apply from: 'gradle/credentials.gradle'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/code-quality.gradle'

gradlePlugin {
    plugins {
        jbakePlugin {
            id = project_pluginId
            implementationClass = 'me.champeau.gradle.JBakePlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencies {
    compile gradleApi()
    compile "com.github.zafarkhaja:java-semver:$semverVersion"
    compileOnly "org.jbake:jbake-core:$jbakeVersion"
    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude group:'org.codehaus.groovy'
    }
    testCompile "commons-io:commons-io:$commonsIoVersion"
}

compileGroovy {
    sourceCompatibility = project.sourceCompatibility
    targetCompatibility = project.targetCompatibility
}

groovydoc {
    use             = true
    source          = project.sourceSets.main.allSource
    classpath       = javadoc.classpath
    ext.windowtitle = "$project.name $project.version API"
    ext.doctitle    = "$project.name $project.version API"
    header          = "$project.name $project.version API"
    footer          = "Copyright &copy; ${copyrightYear} the original author or authors. All rights reserved."
    includePrivate  = false
    link 'https://docs.gradle.org/3.4.1/javadoc/', 'org.gradle.'
    link 'http://docs.oracle.com/javase/7/docs/api/', 'java.', 'org.xml.', 'javax.', 'org.w3c.'
}

task sourcesJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    group 'Build'
    description 'An archive of the javadoc'
    classifier 'javadoc'
    from groovydoc.destinationDir
}

jar.finalizedBy sourcesJar
jar.finalizedBy javadocJar

artifacts {
    sourcesJar
    javadocJar
}

task publishRelease(dependsOn: [bintrayUpload, publishPlugins]) {}

project.tasks.withType(Test) {
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    onOutput { descriptor, event ->
        logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }

    testLogging {
        showStandardStreams = true
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Test results ${project.name}: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

jacoco {
    toolVersion = jacocoVersion
}